import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.gradleutils
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
    alias libs.plugins.licenser
    alias libs.plugins.shadow
}

gradleutils.displayName = 'Slime Launcher'
description = 'A slim launcher for Minecraft in the development environment.'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    // Even though older Minecraft supports as far back as 1.5, 8 is compatible with it, and SL is not compiling anything.
    toolchain.languageVersion = JavaLanguageVersion.of(8)

    withSourcesJar()
}

dependencies {
    compileOnly libs.nulls

    implementation libs.gson
    implementation libs.jopt

    implementation libs.bundles.utils
}

license {
    header rootProject.file('LICENSE-header.txt')
    newLine false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Main-Class':            'net.minecraftforge.launcher.Main',
            'Automatic-Module-Name': 'net.minecraftforge.launcher'
        ])

        gradleutils.manifestDefaults(it, 'net/minecraftforge/launcher/')
    }

    archiveClassifier = 'thin'
}

tasks.named('shadowJar', ShadowJar) {
    enableAutoRelocation = true
    relocationPrefix = 'net.minecraftforge.launcher.shadow'

    archiveClassifier = ''
}

changelog {
    fromBase()
}

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('mavenJava', MavenPublication) {
        from components.shadow

        changelog.publish(it)
        gradleutils.promote(it)

        pom { pom ->
            name = gradleutils.displayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }
}
